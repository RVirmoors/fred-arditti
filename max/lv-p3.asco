; F.P. - lvbeet

@insert lvMacros.asco
@insert lvSpat.asco

antescofo::analysis 4096 512
antescofo::suivi 0
antescofo::pedal 0

BPM 60 ; 1 beat = 1 second

$playing := 0 ; for debugging

@start ; reset initial params

loadfile takes/p3-ABC.wav

; ==============


input-sel 3 ; TODO: select 4 ..1=vn1, 2=vn2, 3=va, 4=cl.

	getfreq 1

EVENT 25 A

	$a[0] := 15; tone
	$d[0] := 0.7
	$a[1] := 0	; noise
	$d[1] := 0.5
	$a[2] := 0	; grill
	$d[2] := 0.2
	$a := $a

;	curve @command {spat room 1 reverberance} 20, 90 10
	curve granMirror @Grain := 0.05s, @Action := {gmirror feedbk $x, grain_freq $y}
	{ $x, $y {
			{ 0.9, 128 }
		8	{ 0.08, 99 }
		8	{ 0.05, 25 }
	}}

	@doubler(vn1 2 vn2 2 va 3, 0.5, 555)

	@noiseRec(va 2) ; TODO change to vc?
	@resoGrill(vn1 1 vn2 1 va 1 vc 0.5, -20, 0)	
	curve 1-gain -50, 0.0	3	; fade in Tone
	curve 3-gain -50, 0.0	3	; fade in Grill
	2 ::spat_goto(3, 60, 1, 9)
	2 @noiseStop
	@noiseScreen(vn1 1 vn2 1 va 1.5 vc 1, 2)
	curve 2-gain -30, 0.0	2.1	; fade in Noise
	$spatNoise := ::spat_radial(2)
	$spatTone := ::spat_radial(1)
	::spat_goto(2, 160, 1, 10)
	5 $spatGrill := ::spat_spray(3)
	::spat_goto(3, -225, 2, 5)
	6 abort $spatNoise
	$spatNoise := ::spat_spray(2)
	::spat_goto(1, -40, 1, 10)
	::spat_goto(2, 180, 3, 10)
	::spat_goto(3, 180, 3.5, 10)


if ($playing == 0) {
	demoms 33700
}
EVENT 39 B

	@doubler(vn1 3 vn2 3 va 3 vc 2.5, 0.5, 600)
	$a[0] := 0 ; doubler
	$d[0] := 0.6
	$a := $a

	@noiseScreen(vn1 2 vn2 1 va 2 vc 1, 1.1)
;	$spatNoise := ::spat_spray(2)
	dmirror feedbk 0.9, grain_freq 18, max_delay 250

	@resoScreen(vn1 2 vn2 2)
	abort $spatTone
	repel 1 0
	$spatTone := ::spat_circle_walk(1, 3)


if ($playing == 0) {
	demoms 95000
}
EVENT 10 C
	input-sel 3
	spat room 1 reverberance 90
	spat room 1 size 2000
	abort $spatNoise
	abort $spatGrill
	abort $spatTone
	$spatNoise := ::spat_circle_walk(2, 1)
	@noiseScreen(off, 0)
	$a[0] := 15
	$d[2] := 0.5
	$d[1] := 1.6
	$d := $d
	@mirror(vn1 5 vn2 1 va 1)
	1	@mirror(vn1 1 vn2 1.2 va 1)

;	whenever MIRSPAT ($a[1]) {
;		$a[1] := -$a[2]
;	}
	::spat_goto(3, 55, 1, 10)
	getfreq 1
	@doubler(vn1 1 vn2 1 va 0.5 vc 2.5, 2, 16000)
	@resoGrill(vn1 1 vn2 1 va 0.5, -20, 0)
	0.1 dmirror feedbk 0.7, max_delay 166 
	gmirror feedbk 0.5

EVENT 10 m8 
	::spat_goto(3, -40, 1, 5)
	curve @command {gmirror feedbk} 0.5, 0.8 5

EVENT 5 m10
	::spat_goto(3, -200, 0.7, 3)
	curve @command {dmirror feedbk} 0.7, 0.2 5

EVENT 11 m11
	::spat_goto(3, -155, 1, 4)

EVENT 9 m13
	::spat_goto(3, 15, 0.8, 1)

EVENT 25 m15 
	::spat_goto(3, 333, 2, 15)

EVENT 45 D 

EVENT 155 E
loadfile takes/p3-E1.wav

	1-gain 0
	2-gain 0
	3-gain 0
	spat room 1 size 1000
	spat room 1 reverberance 65	
	abort $spatNoise
	$a := [0, -90, 90]
	$d := [1, 1.5, 1]
	$a := $a

	@mirror(vn2 1 va 1)
	abort ENMIR
	@resoGrill(vn1 1 vc 1, -15, 0)

	$spatMirr := ::spat_circular(2, 1)
	$spatGrill := ::spat_spray(3)
	whenever MIRSPAT ($a[1]) {
		$a[2] := -$a[1]
	}

	curve DELAYS @Grain := 0.05s, @Action := {
		dmirror scrub_freq $sfr
		dmirror max_delay $md
		$d[1] := ($sfr * 1.2)
	}
	{ $sfr, $md {
			{ 0.66, 500}
		5	{ 0.01, 100 }
		5	{ 5	  , 50 }
	}} ;TODO spat & envelopes for more pages

EVENT 39 F-end ;TODO check for leaks

	@doubler(vn1 3 vn2 3 va 3 vc 2.5, 0.5, 600)
	$a[0] := 0 ; doubler
	$d[0] := 0.6
	$a := $a

	@noiseScreen(vn1 2 vn2 1 va 2 vc 1, 1.1)
;	$spatNoise := ::spat_spray(2)
	dmirror feedbk 0.9, grain_freq 18, max_delay 250

	@resoScreen(vn1 2 vn2 2)
	abort $spatTone
	repel 1 0
	$spatTone := ::spat_circle_walk(1, 3)