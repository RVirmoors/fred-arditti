; F.P. - lvbeet

@insert lvMacros.asco
@insert lvSpat.asco

antescofo::analysis 4096 512
antescofo::suivi 0
antescofo::pedal 0


BPM 60 ; 1 beat = 1 second

$playing := 0 ; for debugging

@start ; reset initial params

loadfile takes/p2-A1-3.wav

; ==============


input-gain 8 ; start is very quiet!!!
input-sel 1 ; 1=vn1, 2=vn2, 3=va, 4=cl.

EVENT 15 A
;	$spatReso := ::spat_circular(1, 1) ; start spinning screen1
	$spatNoise := ::spat_radial(2)
;	$spatTone := ::spat_teleport(3)
	@noiseRec(vn2 2)
	4 @noiseStop
	@noiseScreen(vn1 2 vn2 1 va 2 vc 1)
	curve 2-gain -30, 0.0	2.1
	$spatNoise := ::spat_spray(2)
	dmirror feedbk 0.9, grain_freq 18, max_delay 250
NOTE Bb3 3 A2
	antescofo::suivi 1
	$spatRepel2 := ::spat_repelAttacks(2)
NOTE A3 6
	print slide-down
TRILL (A3 G3) 3
	print irr
	3 input-sel 2	; vn2
	@toneScreen(vc 1, 0.97) ; resonate Cello
	curve 1-gain -30, 0.0	4
NOTE Ab4 1/2
	print up
NOTE G3 1/2
	print down
NOTE 0 1
NOTE A5	4
	print notehigh
TRILL (G6 D5 A3) 1
	print irr
	1 input-sel 1
NOTE A5 1
	print FL
NOTE G#5 3
	print delic
	antescofo::suivi 0

if ($playing == 0) {
	loadfile takes/p2-B1-3.wav
}
	input-gain 1
	1-gain 0
	2-gain 0
	
EVENT 70 B
	spat room 1 size 1333	
	abort $spatNoise
	abort $spatRepel2
	$spatMirr := ::spat_cross(2)
	$spatReso := ::spat_cross(3)
	repel 2 0
	$a := [0, -90, 90]
	enable_switch 1
	$switch_button := 1 ; start with mirror
	@mirror(vn1 1 vn2 1 va 1 vc 1)
	@resoGrill(vn1 1 vn2 1 va 1 vc 1) ; [s]witch MANUALLY between the two

	0.2 dmirror grains_on_off 1
	dmirror feedbk 0.07, grain_freq 19.7
	dmirror highpass 13400, lowpass 22000
	dmirror scrub_freq 0.66, max_delay 2000
	gmirror feedbk 0.2
	; scrub_freq is a measure of delay randomness. 
	; we start from a fairly low value (precise repeats) and vary it across the section

	curve DELAYS @Grain := 0.05s, @Action := {
		dmirror scrub_freq $sfr
		dmirror max_delay $md
	}
	{ $sfr, $md {
			{ 0.01, 200 }
		20	{ 0.66, 2000}
	}}

EVENT 70 C
	enable_switch 0
	$switch_button := 0
	@mirror(vn1 1 vn2 1 va 1 vc 1)
	@resoGrill(vn1 0.2 vn2 0.2 va 0.1)
	abort $spatMirr
	abort $spatReso
	$spatMirr := ::spat_circular(2, 1)
	whenever MIRSPAT ($a[1]) {
		$a[2] := -$a[1]
	}

	curve DELAYS @Grain := 0.05s, @Action := {
		dmirror scrub_freq $sfr
		dmirror max_delay $md
		$d[1] := ($sfr * 1.2)
	}
	{ $sfr, $md {
			{ 0.66, 2000}
		50	{ 0.01, 200 }
		20  { 5	  , 50 }
	}}

EVENT 70 D ; back to section A settings
	@noiseScreen(vn1 2 vn2 1 va 2 vc 1)
	$spatNoise := ::spat_spray(2)
	dmirror feedbk 0.9, grain_freq 18, max_delay 250
;EVENT 1 end
